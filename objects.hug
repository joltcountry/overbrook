object penny "Penny"
{
    nouns "penny" "dog" "pitbull"
    in backyard
    initial_desc {"An old gray pit bull named Penny lies in the sun."}
    short_desc {"Penny is lying here in the sun."}
    long_desc 
    {
        "Like most of our dogs, we were supposed to foster her for a week.  That
        was ten years ago."
    }
    before
    {
        object DoGet
        {
            "She's happy right where she is."
        }
        object DoPet
        {
            "She rolls over to receive your generous belly rubs."
            if (penny is not pet)
            {
                scoreit("blah blah")
                penny is pet
            }
        }
    }
}

object garagedoor "garage door"
{
    found_in garage, upperdrive
    is not open, openable, static
    long_desc {
        print "It is unmistakeably a garage door, which is currently ";
        if (self is open) {
            "open."
        } else {
            "closed."
        }
    }
    article "the"
    adjective "garage"
    noun "door"
    before
    {
        object DoOpen {
            "It is too inconvenient to open manually.  You will have to find another way."
            return true
        }
        object DoClose {
            "It is too inconvenient to close manually.  You will have to find another way."
            return true
        }
    }
}

object garagebutton "button"
{
    nouns "button" "opener"
    adjectives "garage"
    long_desc
    {
        "About three inches wide and one inch high, this rectangular button seems
        destined to be pressed."
    }
    in garage
    is hidden, static
    before
    {
        object DoPress
        {
            if garagedoor is open
            {
                garagedoor is not open
                "With a mechanical whir, the garage door slowly comes down.";
                garage.graphic = {
                    current_resource_file = "PICS"
                    current_resource = "garageclosed"
                    return -1
                }
            } else {
                garage.graphic.current_resource = "garageopen"
                garagedoor is open
                "With a mechanical whir, the garage door slowly opens.  ";
                garage.graphic = {
                    current_resource_file = "PICS"
                    current_resource = "garageopen"
                    return -1
                }
            }
            if self is not moved
            {
                print "Congratulations, you have discovered the button's powerful secret!"
                scoreit()   
                self is moved
            } else {
                ""
            }
            return true
        }
    }
}

object mailbox "mailbox"
{
    capacity 2 
    nouns "mail" "box" "mailbox"
    in lowerdrive
    is container, openable, not open
    article "a"
    long_desc {
        print "A box, elevated by a short pole, ostensibly for the purpose of receiving and sending mail.
        It is currently ";
        if self is open {
            "open."
        } else {
            "closed."
        }
    }
}

object authornote "note from the author"
{
    size 1
    in mailbox
    nouns "note"
    adjective "author's" "author"
    article "a"
    long_desc {"It is a note from the author.  Up to you whether you read it or not."}
    before {
        object DoRead
        {
            if self not in player
            {
                "It would be much easier to read if you were carrying it."
                return true
            }

            "Thanks for taking the Overbrook tour!  I hope you've enjoyed it as much as I've
            enjoyed bringing to you.  Hopefully more.  Much more.  Jesus Christ, what a pain
            in the ass this is."
            if self is not read
            {
                self is read
                scoreit()
            }
        }
    }
}

routine feedBunny
{
    if (treats in player) {
        if dinky is moved {
            "He's fat enough as it is."
        } else {
            dinky is moved
            print "Dinky quickly gobbles up the treats, without even acknowledging your generosity."
            scoreit
        }
    } else {
        "You have nothing he wants."
    }
}

routine feedCat
{
    if (treats in player) {
        "She is a small animal, but does not like these small animal treats."
    } else {
        "You have nothing she wants."
    }
}

object dinky "Dinky the bunny rabbit"
{
    in office
    nouns "bunny" "rabbit" "dinky"
    adjectives "white" "fat"
    initial_desc {"Dinky the bunny rabbit is here, hopping around his gated-off area."}
    long_desc
    {
        "Dinky is a fat white Californian rabbit.  Like all the other animals in the house, 
        he was not supposed to be here more than a couple weeks, and it's now been five years."
    }
    before
    {
        object DoGet 
        {
            "Please do not pick up our animals during your tour."
        }
        object DoPet 
        {
            "You're able to run your hands over his ears for a second before he hops away."
        }
        object DoFeed 
        {
            feedBunny
        }
        object DoFeedWith
        {
            if (xobject = treats) {
                feedBunny
            }
        }
        xobject DoFeedTo
        {
            if (object = treats) {
                feedBunny
            }
        }        
    }
}

object cat "cat"
{
    in foyer
    nouns "cat"
    adjectives "white"
    is hidden
    long_desc
    {
        "Skyler, the smelly white cat, is taking a break from puking and shitting on the floor."
    }
    before
    {
        object DoGet 
        {
            "Please do not pick up our animals during your tour."
        }
        object DoPet 
        {
            "She purrs quietly and drools on your hand a little bit.  Gross."
        }
        object DoFeed 
        {
            feedCat
        }
        object DoFeedWith
        {
            if (xobject = treats) {
                feedCat
            }
        }
        xobject DoFeedTo
        {
            if (object = treats) {
                feedCat
            }
        }        
    }
}

object treats "bag of treats"
{
    nouns "bag" "treats"
    article "a"
    adjectives "animal" "small"
    in table
    long_desc {"A deligtful mix of seeds and dried fruit."}
}

object table "table"
{
	in office
	nouns "table"
	article "a"
	capacity 50
	holding 0
	is platform, static
    short_desc {"A table sits in the corner."}
    long_desc {"It's less a table than a support mechanism for hamster cages."}
}

object hamsters "hamsters"
{
    in table
    is static, hidden
    nouns "hamsters" "cage" "cages" "hamster"
    long_desc {"We bought two hamsters.  Turns out they were both female.  Turns 
                out they were both pregnant.  So now we have lots of them."}
    before
    {
        object DoGet {"If it was up to me, you could take 'em all.  But it ain't."}
    }
}

object honda "Honda Passport"
{
    in upperdrive
    is static, hidden
    nouns "car" "honda" "van" "passport"
    article "the"
    long_desc {"\"Daddy's car\", a dark gray 2021 Honda Passport.  Used mainly for going to the grocery store."}
    before
    {
        object {
            if verbroutine ~= &DoLook {
                "Please leave Daddy's car alone."
                return true
            }
            return false
        }
    }
}

object jeep "Jeep Wrangler"
{
    in garage
    is static, hidden
    nouns "car" "jeep" "wrangler"
    article "the"
    long_desc {"\"Mommy's car\", a dark green 2022 Jeep Wrangler.
                It's important for a family where both adults work from home to have two cars."}
    before
    {
        object {
            if verbroutine ~= &DoLook {
                "Please leave Mommy's car alone."
                return true
            }
            return false
        }
    }
}